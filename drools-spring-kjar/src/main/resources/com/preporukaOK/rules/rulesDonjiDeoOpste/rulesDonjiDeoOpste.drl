package com.preporukaOK.rules.rulesDonjiDeoOpste

import com.ftn.PreporukaOdevneKombinacije.model.*;
import com.ftn.PreporukaOdevneKombinacije.model.enums.*;
import com.ftn.PreporukaOdevneKombinacije.dto.*;
import com.ftn.PreporukaOdevneKombinacije.model.drlModel.*;

import java.util.List;
import java.util.ArrayList;

import java.util.HashMap;

import accumulate com.ftn.PreporukaOdevneKombinacije.droolsFunction.SumPerClothingItem sumPerItem;

declare TipTelaIzabran
	tipTela: TipTela
end

declare TipDonjegDelaIzabran
    tipDonjeg : DonjiDeoEnum[]
end

declare PodtipDonjegDelaIzabran
    podtipDonjegDela : TipDonjegDela[]
end

declare MaterijalIzabran
	materijal: Materijal
end

declare DuzinaIzabrana
	duzina: DuzinaDonjegDela[]
end

declare DubinaIzabrana
	dubina: Dubina[]
end

declare BojaIzabran
    boja : Boja
end

declare BojaIntenzitetIzabran
	intenzitet: BojaIntenzitet
end

declare Marker
    uniqueIdentifier : String
end

global HashMap<Boja, Integer> hashMapColor

rule "Boja - Dodavanje bodova za tip odece"
    salience ((1/$poeni)*1000)
    agenda-group "bojeMapa"
    no-loop
    when
        $gornjiDeo : PreporuceniGornjiDeo($poeni : poeni);
    then
        hashMapColor.put($gornjiDeo.getGornjiDeo().getBoja(),$poeni);
    	System.out.println("Boja mapa");
end

rule "A1 - Tip tela - trougao"
    salience 500
    when
        $unos : UnosNeulogovanDTO(kukovi >= ramena + 5/100 * kukovi);
    then
    	insert(new TipTelaIzabran(TipTela.TROUGAO));
        System.out.println("Tip tela - trougao");
end

rule "A2 - Tip tela - pravougaonik"
    salience 490
    when
        $unos : UnosNeulogovanDTO( ramena <= 25/100 * ramena + struk, ramena <= 5/100 * ramena + kukovi);
        not (TipTelaIzabran())
    then
    	insert(new TipTelaIzabran(TipTela.PRAVOUGAONIK));
        System.out.println("Tip tela - pravougaonik");
end

rule "A3 - Tip tela - obrnuti trougao"
    salience 500
    when
        $unos : UnosNeulogovanDTO( ramena >= kukovi + 5/100 * ramena);
        not (TipTelaIzabran())
    then
    	insert(new TipTelaIzabran(TipTela.OB_TROUGAO));
        System.out.println("Tip tela - ob trougao");
end

rule "A4 - Tip tela - pescani sat"
     salience 500
     when
         $unos : UnosNeulogovanDTO( ramena > 25/100 * ramena + struk, ramena  <= 5/100 * ramena + kukovi);
         not (TipTelaIzabran())
     then
     	insert(new TipTelaIzabran(TipTela.PES_SAT));
         System.out.println("Tip tela - pescani sat");
 end

rule "A5 - Tip tela - krug"
     salience 480
     when
         not(TipTelaIzabran())
     then
     	insert(new TipTelaIzabran(TipTela.KRUG));
         System.out.println("Tip tela - krug");
end

rule "B1 - Tip donjeg dela ako je tip tela trougao"
	salience 450
    no-loop
    when
        $tipTela : TipTelaIzabran(tipTela == TipTela.TROUGAO);
        $unos : UnosNeulogovanDTO(dressCode != DressCode.SPORTSKI);
    then
        insert(new TipDonjegDelaIzabran(new DonjiDeoEnum[]{ DonjiDeoEnum.PANTALONE, DonjiDeoEnum.SUKNJA}));
        insert(new PodtipDonjegDelaIzabran(new TipDonjegDela[] {TipDonjegDela.FLARED,TipDonjegDela.SKINY,TipDonjegDela.PENCIL,TipDonjegDela.ALINE}));
        System.out.println("b1 trougao");
end

rule "B2 - Tip donjeg dela ako je tip tela obrnuti trougao"
	salience 450
    no-loop
    when
        $tipTela : TipTelaIzabran(tipTela == TipTela.OB_TROUGAO);
        $unos : UnosNeulogovanDTO(dressCode != DressCode.SPORTSKI);
    then
        insert(new TipDonjegDelaIzabran(new DonjiDeoEnum[]{ DonjiDeoEnum.PANTALONE, DonjiDeoEnum.SUKNJA}));
        insert(new PodtipDonjegDelaIzabran(new TipDonjegDela[] {TipDonjegDela.PLEATED,TipDonjegDela.TULIP,TipDonjegDela.STRAIGHT,TipDonjegDela.BAGGY}));
        System.out.println("b2 obrnuti trougao");
end

rule "B3 - Tip donjeg dela ako je tip tela PRAVOUGAONIK"
	salience 450
    no-loop
    when
        $tipTela : TipTelaIzabran(tipTela == TipTela.PRAVOUGAONIK);
        $unos : UnosNeulogovanDTO(dressCode != DressCode.SPORTSKI);
    then
        insert(new TipDonjegDelaIzabran(new DonjiDeoEnum[]{ DonjiDeoEnum.PANTALONE, DonjiDeoEnum.SUKNJA}));
        insert(new PodtipDonjegDelaIzabran(new TipDonjegDela[] {TipDonjegDela.PLEATED,TipDonjegDela.TULIP,TipDonjegDela.STRAIGHT}));
        System.out.println("b3 pravougaonik");
end

rule "B4 - Tip donjeg dela ako je tip tela PES_SAT"
	salience 450
    no-loop
    when
        $tipTela : TipTelaIzabran(tipTela == TipTela.PES_SAT);
        $unos : UnosNeulogovanDTO(dressCode != DressCode.SPORTSKI);
    then
        insert(new TipDonjegDelaIzabran(new DonjiDeoEnum[]{ DonjiDeoEnum.PANTALONE, DonjiDeoEnum.SUKNJA}));
        insert(new PodtipDonjegDelaIzabran(new TipDonjegDela[] {TipDonjegDela.WRAP,TipDonjegDela.TRUMPET,TipDonjegDela.SKINY,TipDonjegDela.FLARED}));
        System.out.println("b4 pescanis sat");
end

rule "B5 - Tip donjeg dela ako je tip tela krug"
	salience 450
    no-loop
    when
        $tipTela : TipTelaIzabran(tipTela == TipTela.KRUG);
        $unos : UnosNeulogovanDTO(dressCode != DressCode.SPORTSKI);
    then
        insert(new TipDonjegDelaIzabran(new DonjiDeoEnum[]{ DonjiDeoEnum.PANTALONE, DonjiDeoEnum.SUKNJA}));
        insert(new PodtipDonjegDelaIzabran(new TipDonjegDela[] {TipDonjegDela.PENCIL,TipDonjegDela.ALINE,TipDonjegDela.WRAP,TipDonjegDela.SKINY,TipDonjegDela.BOOTCUT}));
        System.out.println("b5 krug");
end

rule "B6 - dresscode sportski"
	salience 450
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.SPORTSKI);
    then
        insert(new TipDonjegDelaIzabran(new DonjiDeoEnum[]{ DonjiDeoEnum.TRENERKA, DonjiDeoEnum.HELANKE, DonjiDeoEnum.SORC}));
        insert(new PodtipDonjegDelaIzabran(new TipDonjegDela[] {TipDonjegDela.NONE}));
        System.out.println("b5 krug");
end

rule "B - Dodavanje bodova za tip odece"
    salience 400
    no-loop
    when
        $donjiDeo : DonjiDeo();
        $brojponavljanja : Long( this > 0) from accumulate(
                        	PodtipDonjegDelaIzabran( podtipDonjegDela contains $donjiDeo.getOdecaPodTip()),
                        	count(1)
                        );
    then
    	insert(new KoeficijentPreporuke(20 + 5 * $brojponavljanja, $donjiDeo));
        System.out.println("B bodovi");
end

rule "C1 - 1 - Biranje materijala ako je dresscode lezeran "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.LEZERAN );
    then
        insert(new MaterijalIzabran(Materijal.POLIESTER));
        insert(new MaterijalIzabran(Materijal.PAMUK));
        insert(new MaterijalIzabran(Materijal.TEKSAS));
        insert(new MaterijalIzabran(Materijal.SOMOT));
        System.out.println("c1 lezeran");
end

rule "C1 - 2 - Biranje materijala ako je dresscode sportski "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.SPORTSKI );
    then
        insert(new MaterijalIzabran(Materijal.PAMUK));
        insert(new MaterijalIzabran(Materijal.POLIESTER));
        System.out.println("c2 sportski");
end

rule "C1 - 3 - Biranje materijala ako je dresscode formalan "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.FORMALAN );
    then
        insert(new MaterijalIzabran(Materijal.POLIESTER));
        insert(new MaterijalIzabran(Materijal.PAMUK));
        insert(new MaterijalIzabran(Materijal.TEKSAS));
        insert(new MaterijalIzabran(Materijal.CIPKA));
        System.out.println("c3 formalan");
end

rule "C1 - 4 - Biranje materijala ako je dresscode blacktie "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.BLACKTIE );
    then
        insert(new MaterijalIzabran(Materijal.POLIESTER));
        insert(new MaterijalIzabran(Materijal.CIPKA));
        System.out.println("c4 blacktie");
end

rule "C1 - 5 - Biranje materijala ako je dresscode izlazak "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.IZLAZAK );
    then
        insert(new MaterijalIzabran(Materijal.POLIESTER));
        insert(new MaterijalIzabran(Materijal.PAMUK));
        insert(new MaterijalIzabran(Materijal.TEKSAS));
        insert(new MaterijalIzabran(Materijal.CIPKA));
        insert(new MaterijalIzabran(Materijal.KOZA));
        System.out.println("c5 izlazak");
end

rule "C2 - 1 - Biranje materijala ako je leto "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.IZLAZAK || dressCode == DressCode.LEZERAN || dressCode == DressCode.FORMALAN, vreme == 'leto' );
    then
        insert(new MaterijalIzabran(Materijal.LAN));
        System.out.println("c21 leto materijal");
end

rule "C2 - 2 - Biranje materijala ako je prolece/jesen "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.LEZERAN || dressCode == DressCode.FORMALAN, vreme == 'prolece' || vreme == 'jesen');
    then
        insert(new MaterijalIzabran(Materijal.KOZA));
        System.out.println("c22 prolece jesen");
end

rule "C2 - 3 - Biranje materijala ako je zima "
	salience 380
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.LEZERAN || dressCode == DressCode.FORMALAN, vreme == 'zima' );
    then
        insert(new MaterijalIzabran(Materijal.VUNA));
        System.out.println("c23 zima");
end

rule "C - Dodavanje bodova za tip odece"
    salience 350
    no-loop
    when
        $donjiDeo : DonjiDeo();
        $brojponavljanja : Long( this > 0) from accumulate(
                        	MaterijalIzabran( materijal == $donjiDeo.getMaterijal()),
                        	count(1)
                        );
    then
    	insert(new KoeficijentPreporuke(20 + 5 * $brojponavljanja, $donjiDeo));
        System.out.println("C bodovi");
end

rule "D1 - Dubina ako je visina manje od 165"
	salience 300
    no-loop
    when
        $unos : UnosNeulogovanDTO(visina < 165);
    then
        insert(new DubinaIzabrana(new Dubina[]{Dubina.MAXI,Dubina.MIDI}));
        System.out.println("d1 visina niska");
end

rule "D2 - Dubina ako je visina veca od 175"
	salience 300
    no-loop
    when
        $unos : UnosNeulogovanDTO(visina > 175);
    then
        insert(new DubinaIzabrana(new Dubina[]{Dubina.MINI,Dubina.MIDI}));
        System.out.println("d2 visina visoka");
end

rule "D3 - Dubina ako je visina izmedju 165 i 175"
	salience 300
    no-loop
    when
        $unos : UnosNeulogovanDTO(visina >= 165 && visina <=175);
    then
        insert(new DubinaIzabrana(new Dubina[]{Dubina.MAXI,Dubina.MIDI, Dubina.MINI}));
        System.out.println("d3 visina visoka");
end

rule "D - Dodavanje bodova za dubinu odece"
    salience 260
    no-loop
    when
        $donjiDeo : DonjiDeo();
        $brojponavljanja : Long( this > 0) from accumulate(
                        	DubinaIzabrana( dubina contains $donjiDeo.getDubina()),
                        	count(1)
                        );
    then
    	insert(new KoeficijentPreporuke(20 + 5 * $brojponavljanja, $donjiDeo));
        System.out.println("D bodovi");
end

rule "E1 - Duzina ako dresscode utice"
	salience 300
    no-loop
    when
        $unos : UnosNeulogovanDTO(dressCode == DressCode.BLACKTIE || dressCode == DressCode.FORMALAN);
    then
        insert(new DuzinaIzabrana(new DuzinaDonjegDela[]{DuzinaDonjegDela.MAXI,DuzinaDonjegDela.MIDI}));
        insert(new Marker("E1"));
        System.out.println("e1 dresscode");
end

rule "E2-1 - Duzina ako dresscode ne utice visina nisko"
	salience 300
    no-loop
    when
        $unos : UnosNeulogovanDTO(visina < 165);
        not Marker(uniqueIdentifier == "E1")
    then
        insert(new DuzinaIzabrana(new DuzinaDonjegDela[]{DuzinaDonjegDela.MAXI,DuzinaDonjegDela.MINI}));
        System.out.println("e2 nisko");
end

rule "E2-2 - Duzina ako dresscode ne utice visina srednje ili visoko"
	salience 300
    no-loop
    when
        $unos : UnosNeulogovanDTO(visina >=165);
        not Marker(uniqueIdentifier == "E1")
    then
        insert(new DuzinaIzabrana(new DuzinaDonjegDela[]{DuzinaDonjegDela.MAXI,DuzinaDonjegDela.MIDI, DuzinaDonjegDela.MINI}));
        System.out.println("e2 srednje");
end


rule "E - Dodavanje bodova za dubinu odece"
    salience 250
    no-loop
    when
        $donjiDeo : DonjiDeo();
        $brojponavljanja : Long( this > 0) from accumulate(
                        	DuzinaIzabrana( duzina contains $donjiDeo.getDuzina()),
                        	count(1)
                        );
    then
    	insert(new KoeficijentPreporuke(20 + 5 * $brojponavljanja, $donjiDeo));
        System.out.println("E bodovi");
end

rule "F1-1 boje korisnik nizak"
    salience 150
    no-loop
    when
        $unos : UnosNeulogovanDTO( visina < 165 );
    then
        insert(new Marker("F1-1"));
        System.out.println("F1-1");
        System.out.println(hashMapColor.get(Boja.BELA));
end

rule "F2-1 boje dresscode ne utice prolece"
    salience 140
    no-loop
    when
        $unos : UnosNeulogovanDTO( visina >= 165, vreme == 'prolece' );
    then
        insert(new BojaIzabran(Boja.SIVA));
        insert(new BojaIzabran(Boja.BELA));
        insert(new BojaIzabran(Boja.PLAVA));
        insert(new BojaIzabran(Boja.ZELENA));
        insert(new BojaIzabran(Boja.ROZA));
        insert(new BojaIzabran(Boja.LJUBICASTA));
        System.out.println("F2-1");
end

rule "F2-2 boje dresscode ne utice leto"
    salience 140
    no-loop
    when
        $unos : UnosNeulogovanDTO( visina >= 165, vreme == 'leto' );
    then
        insert(new BojaIzabran(Boja.SIVA));
        insert(new BojaIzabran(Boja.BELA));
        insert(new BojaIzabran(Boja.PLAVA));
        insert(new BojaIzabran(Boja.NARANDZASTA));
        insert(new BojaIzabran(Boja.ROZA));
        insert(new BojaIzabran(Boja.LJUBICASTA));
        System.out.println("F2-2");
end

rule "F2-3 boje dresscode ne utice jesen"
    salience 140
    no-loop
    when
        $unos : UnosNeulogovanDTO( visina >= 165, vreme == 'jesen' );
    then
        insert(new BojaIzabran(Boja.CRNA));
        insert(new BojaIzabran(Boja.SIVA));
        insert(new BojaIzabran(Boja.PLAVA));
        insert(new BojaIzabran(Boja.NARANDZASTA));
        insert(new BojaIzabran(Boja.ZUTA));
        System.out.println("F2-3");
end

rule "F2-4 boje dresscode ne utice zima"
    salience 140
    no-loop
    when
        $unos : UnosNeulogovanDTO( visina >= 165, vreme == 'zima' );
    then
        insert(new BojaIzabran(Boja.CRNA));
        insert(new BojaIzabran(Boja.SIVA));
        insert(new BojaIzabran(Boja.LJUBICASTA));
        insert(new BojaIzabran(Boja.PLAVA));
        System.out.println("F2-4");
end

rule "F - Dodavanje bodova za boju"
    salience 100
    no-loop
    when
        $donjiDeo : DonjiDeo();
        $brojponavljanja : Long( this > 0) from accumulate(
                        	BojaIzabran( boja == $donjiDeo.getBoja()),
                        	count(1)
                        );
    then
    	insert(new KoeficijentPreporuke(20 + 5 * $brojponavljanja, $donjiDeo));
        System.out.println("F bodovi");
end

rule "G1 - Biranje komada prema koeficijentu preporuke"
	salience ($poeni*10)
	lock-on-active
    when
        not Marker(uniqueIdentifier == "F1-1")
    	$donjiDeo : DonjiDeo();
    	accumulate($koef : KoeficijentPreporuke($b4:Double.valueOf(bodovi * komad.getKoeficijentOdabira()) ), $ukupneVrednosti : sumPerItem($koef) );
    	Number($avg : doubleValue) from accumulate( $d : Double() from $ukupneVrednosti ,
                       	average($d) );
        $poeni : Double(this >= $avg) from accumulate(
                	KoeficijentPreporuke(komad.getId() == $donjiDeo.getId(), $b1:Double.valueOf(bodovi * komad.getKoeficijentOdabira())),
                	sum($b1)
                );
        $preporuceniKomadi : PreporuceniKomadi();
    then
        modify($preporuceniKomadi) {getPreporuceniDonjiDelovi().add(new PreporuceniDonjiDeo($donjiDeo, $poeni));}
        System.out.println($ukupneVrednosti);
    	System.out.println("G Biranje komada prema koeficijentu preporuke, izabran komad : " + $donjiDeo.getId() + " broj poena : " + $poeni + "  srednja vrednost poena : " + $avg);
end

rule "G2 - Biranje komada prema koeficijentu preporuke"
	salience (hashMapColor.get($boja))
	lock-on-active
    when
        Marker(uniqueIdentifier == "F1-1")
    	$donjiDeo : DonjiDeo($boja : boja);
    	accumulate($koef : KoeficijentPreporuke($b4:Double.valueOf(bodovi * komad.getKoeficijentOdabira()) ), $ukupneVrednosti : sumPerItem($koef) );
    	Number($avg : doubleValue) from accumulate( $d : Double() from $ukupneVrednosti ,
                       	average($d) );
        $poeni : Double(this >= $avg) from accumulate(
                	KoeficijentPreporuke(komad.getId() == $donjiDeo.getId(), $b1:Double.valueOf(bodovi * komad.getKoeficijentOdabira())),
                	sum($b1)
                );
        $preporuceniKomadi : PreporuceniKomadi();
    then
        modify($preporuceniKomadi) {getPreporuceniDonjiDelovi().add(new PreporuceniDonjiDeo($donjiDeo, $poeni));}
        System.out.println($ukupneVrednosti);
    	System.out.println("G2 Biranje komada prema koeficijentu preporuke, izabran komad : " + $donjiDeo.getId() + " broj poena : " + $poeni + "  srednja vrednost poena : " + $avg);
end

rule "Brisanje poena nakon odabira"
	salience -20
	no-loop
	when
		$koeficijentPreporuke:KoeficijentPreporuke();
		$preporuceniKomadi : PreporuceniKomadi();
	then
		delete($koeficijentPreporuke);
end

rule "Provera uspesnosti"
	salience -30
	no-loop
    when
        preporuceniKomadi : PreporuceniKomadi( preporuceniDonjiDelovi.size > 0);
    then
    	System.out.println("Uspesno zavrsena preporuka");
end