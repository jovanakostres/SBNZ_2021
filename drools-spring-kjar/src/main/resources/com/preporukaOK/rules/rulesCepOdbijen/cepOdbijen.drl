package com.preporukaOK.rules.rulesCepOdbijen

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Set;
import java.util.HashSet;
import java.util.HashMap;
import com.ftn.PreporukaOdevneKombinacije.model.*;
import com.ftn.PreporukaOdevneKombinacije.model.drlModel.*;
import com.ftn.PreporukaOdevneKombinacije.model.enums.*;
import com.ftn.PreporukaOdevneKombinacije.model.event.*;
import com.ftn.PreporukaOdevneKombinacije.dto.*;
import org.kie.api.runtime.rule.Match

declare window petdana
	@doc("Odbijanja poslednjih 5 dana")
	OdbijenKomadEvent() over window:time( 5d )
end

declare DeaktiviranKomad
    @event()
    @expires(14d)
    komad: KomadOdece
end

rule "Odbijen tri puta poslednjih 5 dana"
    salience 100
    agenda-group "deaktiviranje"
	when
		$odbijeni : OdbijenKomadEvent( $komadOdece : komadOdece );
		KomadOdece( id == $komadOdece.getId(), aktivan == true);
		accumulate(
        			$e : OdbijenKomadEvent( komadOdece.getId() == $komadOdece.getId()) from window petdana;
        			$cnt : count( 1 ); $cnt >= 3
        		)
	then
        insert(new DeaktiviranKomad($komadOdece));
        modify($komadOdece) {setAktivan(false);}
        System.out.println("Deaktiviran komad : " + $komadOdece.getId() + "     " + $cnt);
end

rule "Aktiviraj komad 14 dana posle deaktivacije"
    timer ( int: 1s)
    agenda-group "aktiviranje"
    lock-on-active
when
    $k : DeaktiviranKomad( $komadOdece : komad, komad.isAktivan() == false )
    KomadOdece( id == $komadOdece.getId(), aktivan == false);
then
    modify($komadOdece) {setAktivan(true);}
    System.out.println("Aktiviran komad : " + $komadOdece.getId());
end